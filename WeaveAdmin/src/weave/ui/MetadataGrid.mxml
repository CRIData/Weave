<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml"
    xmlns:ui="weave.ui.*"
    xmlns="weave.ui.*"
    width="100%" height="100%">
    <mx:DataGrid id="grid" editable="true" itemEditEnd="editEndHandler(event)" change="changedItemSelect(event)"/>
    <mx:HBox>
        <ui:TextInputWithPrompt id="newPropName" prompt="New Property Name" enter="addProp()"/>
        <mx:Button id="addPropButton" label="Add Property" click="addProp()"/>
        <mx:Button id="delPropButton" label="Delete Property" click="delProp()" 
            enabled="{grid.selectedItem}"/>
    </mx:HBox>
<mx:Script>
<![CDATA[
    import weave.services.AdminInterface;
    import weave.services.beans.AttributeColumnInfo;
    import weave.ui.EntityTreeNode;
    
    import weave.core.UIUtils;
    
    import mx.controls.dataGridClasses.DataGridColumn; 
    import mx.events.DataGridEvent;
    import mx.events.ListEvent;
    import mx.collections.ListCollectionView;

    
    public function handleCreationComplete():void
    {
        var propColumnHeader:DataGridColumn = new DataGridColumn("Property");
        var valColumnHeader:DataGridColumn = new DataGridColumn("Value");
        propColumnHeader.dataField = "property";
        valColumnHeader.dataField = "value";
        propColumnHeader.editable = false;
        valColumnHeader.editable = true;
        grid.columns = [propColumnHeader, valColumnHeader];
    }
    [Bindable(event="metaChanged")] public function get metadata():Object
    {
        /* Convert from the grid's dataProvider to an object that is expected. */
        var lcv:ListCollectionView = grid.dataProvider as ListCollectionView;
        if (lcv == null) return {};
        var rows:Array = lcv.toArray();
        var destmeta:Object = new Object();
        for each (var row:Object in rows)
        {
            if (!row["removed"])
                destmeta[row.property] = row.value;
            else 
                destmeta[row.property] = null;
        }
        return destmeta;
    }
    public function set metadata(srcmeta:Object):void
    {
        /* Convert from an object to a format expected by the dataProvider */
        var rows:Array = new Array();
        for (var prop:String in srcmeta)
            rows.push({property: prop, value: srcmeta[prop]});
        grid.dataProvider = rows;
        dispatchEvent(new Event("metaChanged"));
    }
    public function editEndHandler(event:DataGridEvent):void
    {
        var row:Object = grid.dataProvider[event.rowIndex];
        var newValue:String = grid.itemEditorInstance[grid.columns[event.columnIndex].editorDataField];
        var oldValue:String = row.value;
        if (newValue != oldValue)
        {
            dispatchEvent(new Event("metaChanged"));
        }
    }
    public function addProp():void
    {
        if (newPropName.text) 
        {
            var tmpmeta:Object = metadata;
            tmpmeta[newPropName.text] = "";
            metadata = tmpmeta;
        }
        else
        {
            UIUtils.componentPulse(newPropName);
        }    
    }
    public function delProp():void
    {
        var row:Object = grid.selectedItem;
        if (row["removed"])
            delete(row.removed);
        else
            row["removed"] = true;
        dispatchEvent(new Event("metaChanged"));
    }
    public function changedItemSelect(event:ListEvent):void
    {
        if (!grid.selectedItem) return;
        if (grid.selectedItem["removed"])
        {
            delPropButton.label = "Undo Delete";
        }
        else
        {
            delPropButton.label = "Delete Property";
        }
    }
]]>
</mx:Script>
</mx:VBox>

