<?xml version="1.0" encoding="utf-8"?>
<mx:HDividedBox xmlns:mx="http://www.adobe.com/2006/mxml"
                xmlns:ui="weave.ui.*"
                xmlns:aci="weave.services.beans.*"
                xmlns="weave.ui.admin.managers.*"
                label="Data Hierarchies{metadataEditor.isChanged?'*':''}"
                creationComplete="handleCreated()">
    <mx:VBox height="100%" width="100%" enabled="{!metadataEditor.isChanged}">
		<ui:IndentTextInput label="Tables" labelFontWeight="bold" prompt="Search" id="tableSearch" width="100%"/>
        <ui:EntityTree id="tableTree" rootFilterType="{Entity.TYPE_TABLE}" nodeFilterFunction="{filterTables}"
			change="deleteTableButton.enabled = tableTree.selectedItem &amp;&amp; (tableTree.selectedItem as EntityNode).getEntity().type == Entity.TYPE_TABLE;"
            width="100%" height="100%" dragEnabled="true" dragMoveEnabled="false" dropEnabled="false"/>
        <mx:HBox width="100%">
			<mx:Spacer width="100%"/>
            <mx:Button id="deleteTableButton" label="Delete table" click="deleteTable()"/>
			<mx:Button label="Refresh" click="Admin.entityCache.clearCache();"/>
        </mx:HBox>
    </mx:VBox>
    <mx:VBox height="100%" width="100%" enabled="{!metadataEditor.isChanged}">
        <mx:Label text="Custom hierarchies" fontWeight="bold"/>
        <ui:EntityTree id="hierarchyTree" rootFilterType="{Entity.TYPE_HIERARCHY}"
            width="100%" height="100%" 
            dragEnabled="true" dragMoveEnabled="true" dropEnabled="true"/>
        <mx:HBox width="100%">
			<mx:Spacer width="100%"/>
			<mx:HBox enabled="{selectedItems.length == 1}">
    	        <ui:TextInputWithPrompt id="categoryNameInput" prompt="New category name" width="100%"/>
	            <mx:Button label="Add category" click="addCategory()" enabled="{Boolean(categoryNameInput.text)}"/>
			</mx:HBox>
            <mx:Button label="Remove" click="remove()" enabled="{hierarchyTree.selectedItems.length > 0}"/>
        </mx:HBox>
    </mx:VBox>
	<ui:MetadataEditor id="metadataEditor" height="100%" width="100%" targetItems="{selectedItems}"/>
<mx:Script>
<![CDATA[
	import mx.events.ListEvent;
	
	import weave.core.UIUtils;
	import weave.services.Admin;
	import weave.services.beans.Entity;
	import weave.ui.EntityNode;
	import weave.ui.EntityTree;
	import weave.utils.EventUtils;
	import weave.utils.PopUpUtils;

    [Bindable] public var selectedItems:Array;
	
    public function handleCreated():void
    {
		debugId(this);
		
        tableTree.addEventListener(ListEvent.CHANGE, selectionsChanged);
        hierarchyTree.addEventListener(ListEvent.CHANGE, selectionsChanged);
		
		EventUtils.addDelayedEventCallback(tableSearch, Event.CHANGE, tableTree.refresh, 100);
    }
	
	private function filterTables(item:EntityNode):Boolean
	{
		if (!tableSearch.text)
			return true;
		return item.label.toLowerCase().search(tableSearch.text.toLowerCase()) >= 0;
	}
	
    public function deleteTable():void
    {
		//TODO: ask for password to confirm?
		
        /* Pop up confirmation, then delete tables. */
        PopUpUtils.confirm(this, "Delete Table", "Delete selected tables? All data columns will also be deleted from all associated categories.", deleteTableFunction, null, "Delete", "Cancel")
        function deleteTableFunction():void
        {
			for each (var item:EntityNode in tableTree.selectedItems)
			{
				Admin.entityCache.delete_entity(item.id);
	        }
			tableTree.selectedItem = null;
		}
    }
    public function addCategory():void
    {
		var name:String = categoryNameInput.text
        if (!name)
        {
            UIUtils.componentPulse(categoryNameInput);
            return;
        }
		if (selectedItems && selectedItems.length == 1)
		{
			var node:EntityNode = selectedItems[0];
	        Admin.entityCache.add_category(name, node.id);
    	    categoryNameInput.text = "";
		}
    }
    public function selectionsChanged(event:ListEvent):void
    {
		var tree:EntityTree = event.currentTarget as EntityTree;
        this.selectedItems = tree.selectedItems;
        if (tree == hierarchyTree) 
            tableTree.selectedItem = null;
        else 
            hierarchyTree.selectedItem = null;
    }
    public function remove():void
    {
		for each (var item:EntityNode in hierarchyTree.selectedItems)
		{
			if (item.getEntity().type == Entity.TYPE_HIERARCHY)
			{
	            PopUpUtils.confirm(this, "Remove hierarchy items", 'You have selected some root hierarchies.  Remove all selected items?', removeImmediately, null, "Remove", "Cancel")
				return;
			}
		}
		removeImmediately();
	}
	private function removeImmediately():void
	{
		for each (var item:EntityNode in hierarchyTree.selectedItems)
		{
	        var type:int = item.getEntity().type;
	        var parent:EntityNode = hierarchyTree.getParentItem(item);
	        if (parent)
	        {
				Admin.entityCache.remove_child(parent.id, item.id);
	        }
	        else if (type == Entity.TYPE_HIERARCHY)
	        {
				Admin.entityCache.delete_entity(item.id);
	        }
	        else
	        {
				// this occurs when root item is selected.. should never happen
				weaveTrace("Entity " + item.id + " has no parent and is not a hierarchy");
	        }
		}
		hierarchyTree.selectedItem = null;
		selectedItems = [];
    }
]]>
</mx:Script>
</mx:HDividedBox>
