<?xml version="1.0" encoding="utf-8"?>
<!--
/*
    Weave (Web-based Analysis and Visualization Environment)
    Copyright (C) 2008-2011 University of Massachusetts Lowell

    This file is a part of Weave.

    Weave is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License, Version 3,
    as published by the Free Software Foundation.

    Weave is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->
<SliderTool
	xmlns="weave.visualization.tools.*" 
	xmlns:mx="http://www.adobe.com/2006/mxml" 
	xmlns:ui="weave.ui.*"
	xmlns:settings="weave.ui.settings.*"
	xmlns:LinkableObjectList="weave.ui.LinkableObjectList.*"
	verticalScrollPolicy="off"
	width="400" 
	title="Time Slider Tool"
	height="40">

<mx:Script>
		<![CDATA[
			import weave.Weave;
			import weave.api.WeaveAPI;
			import weave.api.data.IDataSource;
			import weave.api.getSessionState;
			import weave.core.SessionManager;
			import weave.core.weave_internal;
			import weave.data.ColumnReferences.HierarchyColumnReference;
			import weave.utils.HierarchyUtils;
            use namespace weave_internal;

	//constructor
	
	override protected function constructor():void
	{
		super.constructor();
	}
	
	//overrides of abstract SliderTool functions
	
	/**
	 * This function finds a list of attribute tags in the same attribute hierarchy
	 * that have the same @dataTable and @name attributes, then returns a list of
	 * their @year values.
	 * @param ref A reference to an attribute tag in an AttributeHierarchy.
	 * @return An Array of @year values that the referenced column is defined for.
	 */
	override protected function getValuesFromColHier(ref:HierarchyColumnReference):Array
	{
		var path:XML = ref.hierarchyPath.value;
		var attrXML:XML = HierarchyUtils.getNodeFromPath(path, path); // get leaf node
		var dataSource:IDataSource = ref.getDataSource();
		//February 17, 2011  Fixing bug below:  the xml has changed from dataTableName to dataTable
		//var dataTableName:String = attrXML.@dataTableName;
		var dataTableName:String = attrXML.@dataTable;
		var colName:String = attrXML.@name;
		var times:Array = getTimesFromDimension(dataSource, dataTableName, colName);
		if (times.length > 0)
			addToListOfDims(ref);
		return times;
	}
	
	/**
	 * handleLabelChange
	 * This method is called every time there is a change in labels,
	 * so be sure to change labels only as often as is necesary.  
	 * We fill in the hslider labels from this.labelsLinkableString
	 * Since this.labelsLinkableString is a LinkableString it can be changed by
	 * the user by editing the sessionHistory.  
	 * */
	override public function handleLabelChange():void
	{
		//@TODO have values associated with the labels so that the labels can be
		//   changed and the values are still passed into the column request
		if (parent == null)
			return callLater(handleLabelChange);
		var labelsString:String = labelsLinkableString.value || ''; // avoid null pointer error
		dimensionSlider.labels = labelsString.split(',');
		dimensionSlider.minimum = 0;
		dimensionSlider.maximum = dimensionSlider.labels.length - 1;		
		labelsInput.text = labelsString;
	}	
	
	/** 
	 * change - this method is automatically called when the hslider
	 *  is changed.  Here we update all the visualization tools to 
	 *  the new time.  
	 * **/
	override public function handleSliderChange():void
	{
		var newTime:String = dimensionSlider.labels[dimensionSlider.value];
		var descendants:Array = (WeaveAPI.SessionManager as SessionManager).getDescendants(Weave.root, HierarchyColumnReference);
		var len:int = descendants.length;
		for (var i:int = 0; i < len; i++)
		{
			var ref:HierarchyColumnReference = descendants[i] as HierarchyColumnReference;
			var path:XML = ref.hierarchyPath.value;
			var attr:XML = path..attribute[0];
			var yearValue:String = attr.@year;
			if (yearValue.length > 0)
				attr.@year = newTime;
			ref.hierarchyPath.detectChanges();
		}
	}
	
	
	/**
	 * This is specified in the HSlider above - called to get the tooltip that appears when 
	 * the user probes over the slider.  Item is the index of the value that the user is hovering 
	 * over.  
	 **/
	override protected function formatDataTip(item:Number):String 
	{
		var value:String = dimensionSlider.labels[item];
		return value;
	}
	
	//other class methods
	
	/**
	 * For a given datasource/dataTable/columnName, get the year values from the hierarchy
	 * */
	protected static function getTimesFromDimension(dataSource:IDataSource, dataTableName:String, columnName:String):Array
	{
		var times:Array = [];
		if (dataSource != null)
		{
			var hierarchy:XML = getSessionState(dataSource.attributeHierarchy) as XML;
			//get a list of attribute tags containing identical name & dataTableName values
			var attrList:XMLList = hierarchy.descendants("attribute").(String(attribute("dataTable")) == dataTableName && String(attribute("name")) == columnName && attribute("year"));
			// get the year value from each attribute tag
			for (var i:int = 0; i < attrList.length(); i++)
			{
				var year:String = attrList[i].@year.toString();
				if (year.length > 0)
					times.push(year);
			}
		}
		return times;		
	}
	
	

		]]>
	</mx:Script>
</SliderTool>

