<?xml version="1.0" encoding="utf-8"?>
<!--
/*
Weave (Web-based Analysis and Visualization Environment)
Copyright (C) 2008-2011 University of Massachusetts Lowell

This file is a part of Weave.

Weave is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License, Version 3,
as published by the Free Software Foundation.

Weave is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->



<!-- 
/*
	Whomeever this code gets passed onto, please feel free to delete or
	rewrite the comments I have written. They are only painfully obvious
	to facilitate the transferring of this code to the new maintainer.

	~Andrew Wilkinson
*/
-->


<ui:DraggablePanel xmlns="weave.visualization.tools.*"
				   xmlns:mx="http://www.adobe.com/2006/mxml"
				   xmlns:ui="weave.ui.*"
				   xmlns:userControls="weave.ui.userControls.*"
				   creationPolicy="all" layout="absolute">
	<mx:Script>
		<![CDATA[
			import mx.controls.Alert;
			
			import weave.Weave;
			import weave.api.WeaveAPI;
			import weave.api.core.ILinkableHashMap;
			import weave.api.getCallbackCollection;
			import weave.api.getSessionState;
			import weave.api.newDisposableChild;
			import weave.api.registerDisposableChild;
			import weave.api.registerLinkableChild;
			import weave.api.setSessionState;
			import weave.core.SessionManager;
			import weave.core.SessionStateLog;
			import weave.services.collaboration.CollaborationEvent;
			import weave.services.collaboration.CollaborationService;
			import weave.ui.AlertTextBox;
			
			private var collabService:CollaborationService = registerDisposableChild(this, new CollaborationService(Weave.root));
			
			//Guaranteed to run at the creation of the tool. Is used to register all the event listeners.
			override protected function childrenCreated():void
			{	
				//Pops up the control panel when a user opens the tool
				//so that they can type in the server to connect to.
				toggleUserControlPanel();
				
				//Callbakcs to be heard from the collabService, this is how the collabservice
				//can talk back to the Collaboration tool
				collabService.addEventListener(CollaborationEvent.TEXT, handleText);
				collabService.addEventListener(CollaborationEvent.USERS_LIST, handleUserList);
				collabService.addEventListener(CollaborationEvent.DISCONNECT, handleDisconnect);
				
				//UI callbacks, related to the buttons.
				input.addEventListener(KeyboardEvent.KEY_DOWN, sendToRoom);
				sendButton.addEventListener(MouseEvent.CLICK, sendToRoom);
				connectButton.addEventListener(MouseEvent.CLICK, connect);
				disconnectButton.addEventListener(MouseEvent.CLICK, disconnect);
				
			}
			
			//Sends a message from the text box(input) to the server as a TEXT message
			private function sendToRoom(e:Event):void
			{
				if( e is KeyboardEvent && (e as KeyboardEvent).keyCode != Keyboard.ENTER || input.text=="")
					return;
				collabService.sendTextMessage( input.text );
				input.text = "";	//clear the input	
			}
			
			
			//On connection, the config page is removed, and all the buttons that are only available
			//while connected are enabled.
			private function connect(e:MouseEvent):void
			{
				if( username.text == "" || serverIP.text == "" || serverName.text == "" || roomToJoin.text == "" || port.text ==  "" )
				{
					Alert.show( "Can't leave any field blank" );
				}
				else
				{	
					collabService.connect(serverIP.text, serverName.text, int(port.text), roomToJoin.text, username.text);
					cPanel.removePanel();
					input.enabled = true;
					sendButton.enabled = true;
					disconnectButton.enabled = true;
					connectButton.enabled = false;
				}
			}
			
			//Only dissconnect if you're sure you're connected to the server, otherwise
			//collabservice.disconnect() will fail, and than all the buttons will be
			//disabled
			private function disconnect(e:MouseEvent):void
			{
				if( collabService.isConnectedToServer() == true)
				{
					collabService.disconnect();
					input.enabled = false;
					sendButton.enabled = false;
					disconnectButton.enabled = false;
					connectButton.enabled = true;
				}
			}

			//For when a text message is recieved from the server, it is displayed to the log
			//NOTE: I could not get proper scrolling to work once the recieved text messages
			//		went below the height of the log window. 
			private function handleText(e:CollaborationEvent):void
			{
				if (log.verticalScrollPosition == log.maxVerticalScrollPosition)
				{
					log.text += e.getText();
					log.verticalScrollPosition += 100; //always seems to be one line behind...
				}
				else
					log.text += e.getText();	
			}
			
			//Recieved whenever the userlist is updated by someone joining or leaving.
			private function handleUserList( e:CollaborationEvent):void
			{
				users.text = e.getText();
			}
			
			//If the collabService is disconnected for some reason, this ensures the collabUI
			//resets which buttons are disabled and enabled.
			private function handleDisconnect( e:CollaborationEvent):void
			{
				disconnect(null);
			}
		]]>
	</mx:Script>

	
	<mx:VBox width="100%" height="100%" paddingBottom="10" paddingLeft="10" paddingRight="10"
			 paddingTop="10">
		<mx:HBox width="100%" height="100%">
			<mx:TextArea id="log" width="80%" height="100%" editable="false"/>
			<mx:TextArea id="users" height="100%" editable="false"/>
		</mx:HBox>
		<mx:HBox width="100%">
			<mx:TextInput id="input" width="100%" enabled="false"/>
			<mx:Button id="sendButton" label="Send" enabled="false"/>
		</mx:HBox>
	</mx:VBox>
	
	<!--
		The input fields are filled with the defaults from the server I was testing
		on. If you're making your own server, make sure annonymous login is enabled.
	-->
	
	<ui:ControlPanel id="cPanel">
		<mx:VBox label="Visualization Controls">
			<mx:HBox width="100%">
				<mx:Label text="IP:"/>
				<mx:TextInput id="serverIP" width="50%" text="129.63.17.205"/>	
			</mx:HBox>
			<mx:HBox width="100%">
				<mx:Label text="ServerName:"/>
				<mx:TextInput id="serverName" width="50%" text="VAST-SERVER"/>	
			</mx:HBox>
			<mx:HBox width="100%">
				<mx:Label text="Port:"/>
				<mx:TextInput id="port" width="50%" text="5222"/>	
			</mx:HBox>
			<mx:HBox width="100%">
				<mx:Label text="Username:"/>
				<mx:TextInput id="username" width="50%"/>	
			</mx:HBox>
			<mx:HBox width="100%">
				<mx:Label text="Room Name:"/>
				<mx:TextInput id="roomToJoin" width="50%" text="Test"/>	
			</mx:HBox>
			<mx:Button id="connectButton" label="Connect" enabled="true"/>
			<mx:Button id="disconnectButton" label="Disconnect" enabled="false"/>
		</mx:VBox>
	</ui:ControlPanel>
	
</ui:DraggablePanel>
