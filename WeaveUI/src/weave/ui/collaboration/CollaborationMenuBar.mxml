<?xml version="1.0" encoding="utf-8"?>
<!--
	Weave (Web-based Analysis and Visualization Environment)
	Copyright (C) 2008-2011 University of Massachusetts Lowell
	
	This file is a part of Weave.
	
	Weave is free software: you can redistribute it and/or modify
	it under the terms of the GNU General Public License, Version 3,
	as published by the Free Software Foundation.
	
	Weave is distributed in the hope that it will be useful,
	but WITHOUT ANY WARRANTY; without even the implied warranty of
	MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
	GNU General Public License for more details.
	
	You should have received a copy of the GNU General Public License
	along with Weave.  If not, see <http://www.gnu.org/licenses/>.
-->

<mx:HBox xmlns:mx="http://www.adobe.com/2006/mxml" 
		 width="100%" height="25"
		 backgroundAlpha="0"
		 backgroundColor="0x8a8a8a"
		 cornerRadius="7"
		 bottom="0" horizontalGap="0"
		 verticalAlign="middle"
		 creationComplete="creationComplete(event)"
		 removed="onToolbarDestroy()" resize="resizeHandler(event)"
		 horizontalScrollPolicy="off" verticalScrollPolicy="off">
	
	<mx:Spacer width="100%" />
	<mx:Button id="button_connect"  label="Connect"  cornerRadius="0" icon="{iCon}" height="100%" />
	<mx:Button id="button_settings" label="Settings" cornerRadius="0" icon="{iSet}" height="100%" />
	<mx:Button id="button_chat"     label="Chat" 	 cornerRadius="0" icon="{iChat}" width="{this.width/2}" height="100%" />
	
	<mx:Script>
		<![CDATA[
			import mx.controls.Alert;
			import mx.core.Application;
			import mx.core.IFlexDisplayObject;
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.events.MenuEvent;
			import mx.events.ResizeEvent;
			import mx.managers.PopUpManager;
			
			import org.igniterealtime.xiff.conference.RoomOccupant;
			import org.igniterealtime.xiff.events.RoomEvent;
			
			import weave.Weave;
			import weave.api.registerDisposableChild;
			import weave.api.reportError;
			import weave.services.collaboration.CollaborationEvent;
			import weave.services.collaboration.CollaborationService;
			
			public const collabService:CollaborationService = registerDisposableChild(this, new CollaborationService(Weave.root));
			
			private var CHAT:CollaborationChatWindow 			= null;
			private var SETTINGS:CollaborationSettingsWindow 	= null;
			private var connTimer:Timer 						= null;
			private var mouseTimer:Timer						= null;
			private var userMice:Dictionary						= null;
			
			[Embed(source="../../resources/images/collab/connect2.png")] private var iCon:Class;
			[Embed(source="../../resources/images/collab/settings.png")] private var iSet:Class;
			[Embed(source="../../resources/images/collab/chat.png")]	 private var iChat:Class;
			
			protected function creationComplete(event:FlexEvent):void
			{
				trace("CollaborationMenuBar is opened");
				button_connect.enabled 	= false;
				button_chat.enabled 	= false;
				button_settings.enabled = true;

				collabService.addEventListener(CollaborationEvent.LOG, handleLogEvent);
				collabService.addEventListener(CollaborationEvent.DISCONNECT, handleDisconnect);
				collabService.addEventListener(CollaborationEvent.CONNECT, handleConnect);
				collabService.addEventListener(CollaborationEvent.NICK_ERROR, nickConflictError);
				collabService.addEventListener(CollaborationEvent.LOCKED_ERROR, lockedError);
//				collabService.addEventListener(CollaborationEvent.RECONNECT_ERROR, reconnectError);
				collabService.addEventListener(CollaborationEvent.USER_JOINED_CREATE_MOUSE, handleCreateUserMouse);
				collabService.addEventListener(CollaborationEvent.USER_LEFT_REMOVE_MOUSE, handleRemoveUserMouse);
				collabService.addEventListener(CollaborationEvent.USER_REQUEST_MOUSE_POS, handleUserRequestMousePos);
				collabService.addEventListener(CollaborationEvent.USER_UPDATE_MOUSE_POS, updateMousePos);
				collabService.addEventListener(CollaborationEvent.USER_LIST_UPDATED, userListUpdated);
				
				button_connect.addEventListener(MouseEvent.CLICK, connect);
				button_chat.addEventListener(MouseEvent.CLICK, toggleChat);
				button_settings.addEventListener(MouseEvent.CLICK, toggleSettings);
				stage.addEventListener(Event.RESIZE, resizeHandler);
				
				createWindowChat();
				createWindowSettings();
				
				SETTINGS.addEventListener(CollaborationEvent.CONN_SETTINGS_SAVED, settingsConnSaved);
				SETTINGS.addEventListener(CollaborationEvent.ADDON_SETTINGS_SAVED, settingsAddonSaved);
				CHAT.addEventListener(CollaborationEvent.SEND_MESSAGE, sendMessageEventHandler);
				
				resizeHandler();
			}
			private function onToolbarDestroy():void
			{
				trace("CollaborationMenuBar destroyed");
				
				if( collabService.isConnected ) disconnect();
				
				collabService.removeEventListener(CollaborationEvent.LOG, handleLogEvent);
				collabService.removeEventListener(CollaborationEvent.DISCONNECT, handleDisconnect);
				collabService.removeEventListener(CollaborationEvent.CONNECT, handleConnect);
				collabService.removeEventListener(CollaborationEvent.NICK_ERROR, nickConflictError);
				collabService.removeEventListener(CollaborationEvent.LOCKED_ERROR, lockedError);
//				collabService.removeEventListener(CollaborationEvent.RECONNECT_ERROR, reconnectError);
				collabService.removeEventListener(CollaborationEvent.USER_JOINED_CREATE_MOUSE, joinedRoomCreateMouseList);
				collabService.removeEventListener(CollaborationEvent.USER_LEFT_REMOVE_MOUSE, handleRemoveUserMouse);
				collabService.removeEventListener(CollaborationEvent.USER_REQUEST_MOUSE_POS, handleUserRequestMousePos);
				collabService.removeEventListener(CollaborationEvent.USER_UPDATE_MOUSE_POS, updateMousePos);
				collabService.removeEventListener(CollaborationEvent.USER_LIST_UPDATED, userListUpdated);
				if( collabService.room )
					collabService.room.removeEventListener(RoomEvent.ROOM_LEAVE, onTimeout);
				
				button_connect.removeEventListener(MouseEvent.CLICK, connect);
				button_chat.removeEventListener(MouseEvent.CLICK, toggleChat);
				button_settings.removeEventListener(MouseEvent.CLICK, toggleSettings);
				stage.removeEventListener(Event.RESIZE, resizeHandler);
				stage.removeEventListener(MouseEvent.MOUSE_MOVE, mouseMove);
				
				destroyWindowChat();
				destroyWindowSettings();
				
				if( SETTINGS ) {
					SETTINGS.removeEventListener(CollaborationEvent.CONN_SETTINGS_SAVED, settingsConnSaved);
					SETTINGS.removeEventListener(CollaborationEvent.ADDON_SETTINGS_SAVED, settingsAddonSaved);
				}
				if( CHAT ) {
					CHAT.removeEventListener(CollaborationEvent.SEND_MESSAGE, sendMessageEventHandler);
				}
			}
			private function connect(e:MouseEvent = null):void
			{
				button_connect.label = "Connecting...";
				collabService.connect(SETTINGS.collab_SERVERIP, SETTINGS.collab_SERVERNAME, 
					int(SETTINGS.collab_PORT), SETTINGS.collab_ROOMTOJOIN, SETTINGS.collab_USERNAME);
				
				// Set timeout to 10 seconds
				connTimer = new Timer(10000);
				connTimer.addEventListener(TimerEvent.TIMER, serverUnresponsive);
				connTimer.start();
			}
			private function disconnect(e:MouseEvent = null):void
			{
				collabService.disconnect();
			}
			private function serverUnresponsive(e:TimerEvent):void
			{
				stopConTimer();
				updateEnabledSettings();
				
				if( !SETTINGS.visible ) toggleSettings();
				SETTINGS.showConnectionWarning("Server not responding");
			}
			private function createWindowSettings():void
			{
				SETTINGS = PopUpManager.createPopUp(Application.application as Application, CollaborationSettingsWindow) as CollaborationSettingsWindow;
				SETTINGS.hide();
				var p:Point = new Point(button_settings.x + button_settings.width/2, button_settings.y);
				SETTINGS.x = localToGlobal(p).x - SETTINGS.width/2;
				SETTINGS.y = localToGlobal(p).y + button_settings.height;
			}
			private function createWindowChat():void
			{
				CHAT = PopUpManager.createPopUp(Application.application as Application, CollaborationChatWindow) as CollaborationChatWindow;
				CHAT.visible = false;
				var p:Point = new Point(button_chat.x, button_chat.y);
				CHAT.xFrom = localToGlobal(p).x;
				CHAT.xTo   = localToGlobal(p).x;
				CHAT.yFrom = localToGlobal(p).y + button_chat.height;
				CHAT.yTo   = localToGlobal(p).y - CHAT.height;
				CHAT.x     = localToGlobal(p).x;
				CHAT.y	   = localToGlobal(p).y + button_chat.height;
				CHAT.width = button_chat.width;
			}
			private function destroyWindowSettings():void
			{
				if( SETTINGS ) {
					PopUpManager.removePopUp(SETTINGS);
					SETTINGS = null;
				}
			}
			private function destroyWindowChat():void
			{
				if( CHAT ) {
					PopUpManager.removePopUp(CHAT);
					CHAT = null;
				}
			}
			private function toggleSettings(e:MouseEvent = null):void
			{
				if( SETTINGS ) 
					if( SETTINGS.visible )
						SETTINGS.hide();
					else
						SETTINGS.show();
				
				setWindowSettingsPos();
			}
			private function toggleChat(e:MouseEvent = null):void
			{
				if( CHAT )
				{
					CHAT.visible = ( CHAT.visible ) ? false : true;
					if( CHAT.visible ) {
						button_chat.label = "Chat";
						CHAT.unreadMessages = 0;
					}
				}
			}
			private function resizeHandler(event:Event = null):void
			{
				if( SETTINGS ) setWindowSettingsPos();
				if( CHAT )     setWindowChatPos();
			}
			
			private function setWindowSettingsPos():void 
			{
				var p:Point = new Point(button_settings.x + button_settings.width/2, button_settings.y);
				SETTINGS.x = localToGlobal(p).x - (SETTINGS.width/2);
				if( SETTINGS.visible )
					SETTINGS.y = localToGlobal(p).y - SETTINGS.height;
				else
					SETTINGS.y = localToGlobal(p).y + button_settings.height;
			}
			private function setWindowChatPos():void
			{
				var p:Point = new Point(button_chat.x, button_chat.y);
				CHAT.x 	 = localToGlobal(p).x;
				CHAT.xFrom = localToGlobal(p).x;
				CHAT.xTo   = localToGlobal(p).x;
				CHAT.width = button_chat.width;
				CHAT.height= stage.stageHeight/2;
				CHAT.yFrom = localToGlobal(p).y + button_chat.height;
				CHAT.yTo   = localToGlobal(p).y - CHAT.height;
				if( CHAT.visible )
					CHAT.y = localToGlobal(p).y - CHAT.height;
				else
					CHAT.y = localToGlobal(p).y + button_chat.height;
			}
			private function settingsConnSaved(e:Event):void
			{
				button_connect.enabled = true;
			}
			private function settingsAddonSaved(e:Event):void
			{
				/*
				 * Not supported yet!!
				 */
			}
			private function sendMessageEventHandler(e:CollaborationEvent):void
			{
				collabService.sendTextMessage( e.getText() );
			}
			private function handleLogEvent(e:CollaborationEvent):void
			{
				CHAT.logText(e.getText());
				if( !CHAT.visible && collabService.isConnected ) {
					button_chat.label = "Chat [" + CHAT.unreadMessages + "]";
				} else {
					button_chat.label = "Chat";
					CHAT.unreadMessages = 0;
				}
			}
			
			//If the collabService is disconnected for some reason, this ensures the collabUI
			//resets which buttons are disabled and enabled.
			private function handleDisconnect( e:CollaborationEvent):void
			{
				if( mouseTimer ) {
					mouseTimer.stop();
					mouseTimer.removeEventListener(TimerEvent.TIMER, mouseMove);
					mouseTimer = null;
				}
				if( collabService.room )
					collabService.room.removeEventListener(RoomEvent.ROOM_LEAVE, onTimeout);
				updateEnabledSettings();
			}
			private function handleConnect( e:CollaborationEvent ):void
			{
				stopConTimer();
				updateEnabledSettings();
				toggleChat();
				CHAT.setUsername(collabService.username);
				joinedRoomCreateMouseList();
				
				mouseTimer = new Timer(600);
				mouseTimer.addEventListener(TimerEvent.TIMER, mouseMove);
				mouseTimer.start();
				collabService.room.addEventListener(RoomEvent.ROOM_LEAVE, onTimeout);
			}
			private function lockedError(e:Event):void
			{
				stopConTimer();
				if( !SETTINGS.visible ) toggleSettings();
				SETTINGS.showConnectionWarning("Server is locked");
			}
			private function nickConflictError(e:Event):void
			{
				stopConTimer();
				if( !SETTINGS.visible ) toggleSettings();
				SETTINGS.showConnectionWarning("Username already exists");
				updateEnabledSettings();
			}
			private function reconnectError(e:Event):void
			{
				connect();
			}
			private function onTimeout(e:RoomEvent):void
			{
				Alert.show("Would you like to reconnect to the room?", "Disconnection Alert", Alert.YES | Alert.NO, null, disconnectHandler);
			}
			private function disconnectHandler( e:CloseEvent ):void
			{
				if (e.detail == Alert.YES)
					connect();
//					dispatchEvent(new Event(CollaborationEvent.RECONNECT_ERROR));
			}
			private function stopConTimer():void
			{
				if( connTimer ) 
				{
					connTimer.stop();
					connTimer.removeEventListener(TimerEvent.TIMER, serverUnresponsive);
					connTimer = null;
				}
			}
			private function userListUpdated(e:Event):void
			{
				CHAT.userList = collabService.userList;
			}
			private function updateEnabledSettings():void
			{
				if( collabService.isConnected ) {
					button_connect.label = "Disconnect";
					button_connect.removeEventListener(MouseEvent.CLICK, connect);
					button_connect.addEventListener(MouseEvent.CLICK, disconnect);
//					button_connect.setStyle("icon", iDis);
					button_chat.enabled = true;
				} else {
					button_connect.label = "Connect";
					button_connect.removeEventListener(MouseEvent.CLICK, disconnect);
					button_connect.addEventListener(MouseEvent.CLICK, connect);
//					button_connect.setStyle("icon", iCon);
					button_chat.enabled = false;
					if( CHAT.visible ) toggleChat();
				}
			}
			
			private var lastX:Number = 0;
			private var lastY:Number = 0;
			
			private function joinedRoomCreateMouseList():void
			{
				userMice = new Dictionary();
				
				for each( var person:RoomOccupant in collabService.room ) 
				{
					if( person.displayName == collabService.username ) continue;
					trace("asking " + person.displayName + " for mouse pos");
					collabService.requestMouseMessage(person.displayName);
				}
			}
			private function handleCreateUserMouse(e:CollaborationEvent):void
			{
				if( e.getText() == collabService.username ) return;
				if( !userMice )	joinedRoomCreateMouseList();
				
				userMice[e.getText()] = new CollabMouseCursor().addPopUp(Application.application as Application, e.getColor());
			}
			private function handleRemoveUserMouse(e:CollaborationEvent):void
			{
				userMice[e.getText()].removePopUp();
				userMice[e.getText()] = null;
			}
			private function handleUserRequestMousePos(e:CollaborationEvent):void
			{
				collabService.sendMouseMessage(collabService.username, e.getColor(), e.getX(), e.getY());
			}
			private function updateMousePos(e:CollaborationEvent):void
			{
				if( userMice[e.getText()] == null )
				{
					trace( "Creating mouse for "+e.getText());
					userMice[e.getText()] = new CollabMouseCursor().addPopUp(Application.application as Application, e.getColor());
				}
				
//				trace("updating " + e.getText() + " mouse to: ("+e.getX()+","+e.getY()+")");
				userMice[e.getText()].setPos(percentToCoordX(e.getX()), percentToCoordY(e.getY()));
			}
			private function mouseMove(e:TimerEvent):void
			{
				if( collabService.room.length < 2 ) return;				// Your the only one in the room ( dont send )
				if( lastX == mouseX && lastY == mouseY ) return;		// Your mouse never moved ( dont send )

//				trace(collabService.username+" ("+xMousePercent()+"%,"+yMousePercent()+"%)");
				collabService.sendMouseMessage(collabService.username, SETTINGS.collab_COLOR, xMousePercent(), yMousePercent());
				lastX = mouseX;
				lastY = mouseY;
			}
			private function coordToPercentX(x:Number):Number { return x / Application.application.stage.stageWidth;   }
			private function percentToCoordX(x:Number):Number { return x * Application.application.stage.stageWidth;   }
			private function coordToPercentY(y:Number):Number { return y / Application.application.stage.stageHeight;  }
			private function percentToCoordY(y:Number):Number { return y * Application.application.stage.stageHeight;  }
			private function xMousePercent():Number { return Application.application.stage.mouseX / Application.application.stage.stageWidth;  }
			private function yMousePercent():Number { return Application.application.stage.mouseY / Application.application.stage.stageHeight; }

		]]>
	</mx:Script>
</mx:HBox>
