<?xml version="1.0" encoding="utf-8"?>
<mx:HBox  xmlns:mx="http://www.adobe.com/2006/mxml" implements="weave.ui.filterComponents.IFilterComponent"
		 verticalScrollPolicy="off" horizontalScrollPolicy="off">
	<mx:Script>
		<![CDATA[	
			import mx.core.mx_internal;
			import mx.events.FlexEvent;
			
			import weave.api.getCallbackCollection;
			import weave.api.linkBindableProperty;
			import weave.api.newLinkableChild;
			import weave.api.registerLinkableChild;
			import weave.api.setSessionState;
			import weave.core.LinkableString;
			import weave.ui.CustomDataGrid;
			import weave.ui.CustomDataGrid.WeaveCustomDataGridColumn;
			import weave.utils.EventUtils;
			 
			public var column:WeaveCustomDataGridColumn;
			public function mapColumnToFilter(column:WeaveCustomDataGridColumn):void{
				this.column = column;
			}
			
			protected function strToSrchInput_initializeHandler(event:FlexEvent):void{				
				linkBindableProperty(strToSearch, event.target, 'text', 500);
			}
			
			public var regExp:RegExp;			
			public const strToSearch:LinkableString = registerLinkableChild(this, new LinkableString(""),handleChangeInSearchString);
			protected function handleChangeInSearchString():void{				
				regExp = strToRegExp(strToSearch.value);	
				if(column){
					var customDG:CustomDataGrid = column.mx_internal::owner as CustomDataGrid;
					customDG.activateFilters.value = true;//will call **commitProperties**  of dataGrid through **invalidateProperties** for the change in Filter - Property value											
				}
			}
					
			
			
			public function get isActive():Boolean	{
				return (strToSearch.value != null && strToSearch.value != "");
			}
			
			public function filterFunction(obj:Object):Boolean{
				if (!regExp)
					return true;
				return regExp.test(column.itemToLabel(obj));
			}
			
			public static function strToRegExp(strToSrch:String, flags:String = "i", asterisk:Boolean = true, questionMark:Boolean = true):RegExp
			{
				var resultStr:String;			
				//excape metacharacters other than "*" and "?"
				resultStr = strToSrch.replace(/[\^\$\\\.\+\(\)\[\]\{\}\|]/g, "\\$&");			
				//replace strToSrch "?" with reg exp equivalent "."
				resultStr = resultStr.replace(/[\?]/g, ".");			
				//replace strToSrch "*" with reg exp equivalen ".*?"
				resultStr = resultStr.replace(/[\*]/g, ".*?");			
				return new RegExp(resultStr, flags);
			}
			
			
		]]>
	</mx:Script>
	
	<mx:TextInput id="strToSrchInput"  width="100%" height="100%" minWidth="0" initialize="strToSrchInput_initializeHandler(event)"
				 />
		
</mx:HBox >
