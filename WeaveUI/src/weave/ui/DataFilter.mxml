<?xml version="1.0" encoding="utf-8"?>
<!--
/*
    Weave (Web-based Analysis and Visualization Environment)
    Copyright (C) 2008-2011 University of Massachusetts Lowell

    This file is a part of Weave.

    Weave is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License, Version 3,
    as published by the Free Software Foundation.

    Weave is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->
<DraggablePanel xmlns="weave.ui.*" 
				xmlns:mx="http://www.adobe.com/2006/mxml" 
				xmlns:ui="weave.ui.*"
				horizontalScrollPolicy="off" verticalScrollPolicy="off"
				>
	<mx:Script>
		<![CDATA[
			import weave.Weave;
			import weave.api.data.IKeyFilter;
			import weave.api.ui.ILinkableObjectEditor;
			import weave.data.AttributeColumns.DynamicColumn;
			import weave.data.KeySets.NumberDataFilter;
			import weave.data.KeySets.StringDataFilter;
			import weave.utils.ColumnUtils;
			import weave.utils.EditorManager;
			
			private var _editor:ILinkableObjectEditor;
			
			private function get globalName():String
			{
				return Weave.root.getName(this);
			}
			
			private function get keyFilter():IKeyFilter
			{
				return Weave.defaultSubsetKeyFilter.filters.getObject(globalName) as IKeyFilter;
			}
			
			override protected function constructor():void
			{
				super.constructor();
				enableSubMenu.value = true;
			}
			
			override protected function createChildren():void
			{
				super.createChildren();
				
				subMenu.addSubMenuItem(lang("String filter"), setFilterType, [StringDataFilter]);
				subMenu.addSubMenuItem(lang("Number filter"), setFilterType, [NumberDataFilter]);
				
				Weave.defaultSubsetKeyFilter.filters.childListCallbacks.addImmediateCallback(this, handleFilters, true);
			}
			
			override public function dispose():void
			{
				Weave.defaultSubsetKeyFilter.filters.removeObject(globalName);
				super.dispose()
			}
			
			private function setFilterType(type:Class):void
			{
				Weave.defaultSubsetKeyFilter.filters.requestObject(globalName, type, false) as IKeyFilter;
			}
			
			private function handleFilters():void
			{
				if (Weave.defaultSubsetKeyFilter.filters.childListCallbacks.lastNameRemoved == globalName)
				{
					if (_editor && _editor.parent == this)
						this.removeChild(_editor as DisplayObject);
					_editor = null;
					return;
				}
				
				var filter:IKeyFilter = keyFilter;
				if (filter && !_editor)
				{
					_editor = EditorManager.getNewEditor(filter);
					addChild(_editor as DisplayObject);
				}
			}
			
			override protected function get defaultPanelTitle():String
			{
				var column:DynamicColumn;
				
				var ndf:NumberDataFilter = keyFilter as NumberDataFilter;
				if (ndf)
					column = ndf.column;
				
				var sdf:StringDataFilter = keyFilter as StringDataFilter;
				if (sdf)
					column = sdf.column;
				
				if (column)
					return lang('Filter for {0}', ColumnUtils.getTitle(column));
				
				return lang('Filter');
			}
		]]>
	</mx:Script>
</DraggablePanel>
