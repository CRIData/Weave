<?xml version="1.0" encoding="utf-8"?>
<!--
/*
    Weave (Web-based Analysis and Visualization Environment)
    Copyright (C) 2008-2011 University of Massachusetts Lowell

    This file is a part of Weave.

    Weave is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License, Version 3,
    as published by the Free Software Foundation.

    Weave is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with Weave.  If not, see <http://www.gnu.org/licenses/>.
*/
-->
<!--	AlertTextBox
   	*  @author skolman
 	*  Saves the file to the server.
-->

<mx:TitleWindow 
    xmlns:ui ="org.oicweave.ui.*"
    xmlns:mx="http://www.adobe.com/2006/mxml"
	layout="absolute" 
    title="{_title}"
    showCloseButton="false"
    close="handleClose()"
    creationComplete="handleCreationComplete(event)"
    borderAlpha="0.95"
    borderThickness="2"
    horizontalScrollPolicy="off" verticalScrollPolicy="off"
	titleStyleName="weave-panel-title-style"
	styleName="weave-panel-style"
	>
    <mx:Metadata>
    	[Event(name="buttonClicked", type="org.oicweave.ui.AlertTextBoxEvent")]
    	
    </mx:Metadata>
    <mx:Script>
	<![CDATA[
		import mx.core.Application;
		import mx.controls.Button;
		import mx.events.CloseEvent;
		import mx.managers.PopUpManager;
		import mx.printing.FlexPrintJob;
		import mx.printing.FlexPrintJobScaleType;
		import mx.utils.ObjectUtil;
		
		import org.oicweave.data.AttributeColumns.AbstractAttributeColumn;
		import org.oicweave.utils.BitmapUtils;
		
		
		
		//Title of the Dialog Box
		[Bindable]
		private var _title:String = null;
		public override function get title():String
		{
			return _title;
		}
	    public override function set title(value:String):void
		{
			_title = value;
		}
		
		//Message before TextBox
		[Bindable]
		private var _message:String = null;
		public function get message():String
		{
			return _message;
		}
	    public function set message(value:String):void
		{
			_message = value;
		}
		
		//default text in TextBox
		[Bindable]
		private var _textInput:String = null;
		public function get textInput():String
		{
			return _textInput;
		}
	    public function set textInput(value:String):void
		{
			_textInput = value;
		}
	
		private function handleCreationComplete(event:Event):void
		{
			if(_title == null)
			{
				_title = "Alert";
			}
			
			focusManager.setFocus(textBox);
		}		
		
		public static function show(title:String, message:String):AlertTextBox 
		{
			var newAlert:AlertTextBox = PopUpManager.createPopUp(Application.application as DisplayObject, AlertTextBox, true) as AlertTextBox;
			newAlert.title = title;
			// disable moving of the popup
			newAlert.isPopUp = false;
			newAlert.message = message;
			
			PopUpManager.centerPopUp(newAlert);
			
			return newAlert;
		}
		
		private function closeAlertTextBox():void
		{
			PopUpManager.removePopUp(this);
		}
		
		private function handleClose():void
		{
			closeAlertTextBox();
		}		
		
		private function handleOKClick():void
		{
			var okClickEvent:AlertTextBoxEvent = new AlertTextBoxEvent();
			okClickEvent.textInput = _textInput;
			okClickEvent.confirm = true;
			var textclick:Boolean = this.dispatchEvent(okClickEvent);
			closeAlertTextBox();
		}
		
		private function handleCancelClick():void
		{
			var cancelClickEvent:AlertTextBoxEvent = new AlertTextBoxEvent();
			cancelClickEvent.textInput = _textInput;
			cancelClickEvent.confirm = false;
			this.dispatchEvent(cancelClickEvent);
			closeAlertTextBox();
		}
		
		override public function move(x:Number, y:Number):void
		{
			x = Math.min(x, parent.width - this.width);
			y = Math.min(y, parent.height - this.height);
			
			super.move(x, y);
		}
		
   		]]>
	</mx:Script>
	<mx:VBox horizontalAlign="left" width="100%" height="100%" backgroundColor="0xFFFFFF" paddingLeft="5" paddingBottom="5">
		<mx:Label text="{_message}" horizontalCenter="0" width="100%"/>
		<mx:TextInput text="{_textInput}" id="textBox" change="_textInput = textBox.text;" enter="handleOKClick()"/>
		<mx:HBox id="alertButtons">
		<mx:Button label="OK" click="handleOKClick()" emphasized="true"/>
		<mx:Button label="Cancel" click="handleCancelClick()"/>
		</mx:HBox>
	</mx:VBox>

</mx:TitleWindow> 
